// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
  ext {
    buildToolsVersion = findProperty('android.buildToolsVersion') ?: '35.0.0'
    minSdkVersion = Integer.parseInt(findProperty('android.minSdkVersion') ?: '24')
    compileSdkVersion = Integer.parseInt(findProperty('android.compileSdkVersion') ?: '35')
    targetSdkVersion = Integer.parseInt(findProperty('android.targetSdkVersion') ?: '34')
    kotlinVersion = findProperty('android.kotlinVersion') ?: '1.9.24'
    ndkVersion = "26.1.10909125"
  }
  repositories {
    google()
    mavenCentral()
  }
  dependencies {
    classpath("com.android.tools.build:gradle")

    classpath("com.facebook.react:react-native-gradle-plugin")

    classpath("org.jetbrains.kotlin:kotlin-gradle-plugin")

    classpath("com.diffplug.spotless:spotless-plugin-gradle:6.23.3")
  }
}

def reactNativeAndroidDir = new File(
  providers.exec {
    workingDir(rootDir)
    commandLine("node", "--print", "require.resolve('react-native/package.json')")
  }.standardOutput.asText.get().trim(),
  "../android"
)

def jscAndroidDir = new File(
  providers.exec {
    workingDir(rootDir)
    commandLine("node", "--print", "require.resolve('jsc-android/package.json', { paths: [require.resolve('react-native/package.json')] })")
  }.standardOutput.asText.get().trim(),
  "../dist"
)

def expoCameraDir = new File(
  providers.exec {
    workingDir(rootDir)
    commandLine("node", "--print", "require.resolve('expo-camera/package.json')")
  }.standardOutput.asText.get().trim(),
  "../android/maven"
)

allprojects {
  repositories {
    mavenLocal()
    maven {
      // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
      url(reactNativeAndroidDir)
    }
    maven {
      // Android JSC is installed from npm
      url(jscAndroidDir)
    }
    maven {
      url(expoCameraDir)
    }

    google()
    mavenCentral()
    maven { url 'https://jitpack.io' }
  }
}

subprojects {
  plugins.apply("com.diffplug.spotless")
  spotless {
    // note that spotless config is currently duplicated in expo-go too
    kotlin {
      target '**/*.kt'
      ktlint("1.0.1")
          .editorConfigOverride([
              "ktlint_standard_no-wildcard-imports"          : "disabled",
              "ktlint_standard_import-ordering"              : "disabled",
              "ktlint_standard_filename"                     : "disabled",
              "ktlint_standard_property-naming"              : "disabled",
              "ktlint_standard_discouraged-comment-location" : "disabled",
              "ktlint_standard_comment-wrapping"             : "disabled",
              "ktlint_standard_function-naming"              : "disabled",
              "ktlint_standard_class-naming"                 : "disabled",
              "ktlint_standard_package-name"                 : "disabled",
              "ktlint_standard_multiline-expression-wrapping": "disabled",
              "charset"                                      : "utf-8",
              "end_of_line"                                  : "lf",
              "indent_size"                                  : "2",
              "continuation_indent_size"                     : "2",
              "indent_style"                                 : "space",
              "insert_final_newline"                         : "true",
              "tab_width"                                    : "2",
              "trim_trailing_whitespace"                     : "true",
              "ij_kotlin_allow_trailing_comma_on_call_site"  : "false",
              "ij_kotlin_allow_trailing_comma"               : "false"
          ])
      trimTrailingWhitespace()
      indentWithSpaces()
      endWithNewline()
    }
  }
}

apply plugin: "com.facebook.react.rootproject"
