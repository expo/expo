rootProject.name = 'BareExpo'

def reactNativePackage = providers.exec {
  workingDir(rootDir)
  commandLine("node", "--print", "require.resolve('react-native/package.json')")
}.standardOutput.asText.get().trim()

dependencyResolutionManagement {
  versionCatalogs {
    reactAndroidLibs {
      from(files(new File(reactNativePackage, "../gradle/libs.versions.toml")))
    }
  }
}

apply from: new File(
    providers.exec {
      workingDir(rootDir)
      commandLine("node", "--print", "require.resolve('@react-native-community/cli-platform-android/package.json')")
    }.standardOutput.asText.get().trim(),
    "../native_modules.gradle"
)
applyNativeModulesSettingsGradle(settings)

include(":expo-modules-test-core")
project(":expo-modules-test-core").projectDir = new File("../../../packages/expo-modules-test-core/android")

// Include Expo modules
apply from: new File(
    providers.exec {
      workingDir(rootDir)
      commandLine("node", "--print", "require.resolve('expo/package.json')")
    }.standardOutput.asText.get().trim(),
    "../scripts/autolinking.gradle"
)
useExpoModules()

include ':app'
includeBuild(
    file(
        providers.exec {
          workingDir(rootDir)
          commandLine("node", "--print", "require.resolve('@react-native/gradle-plugin/package.json')")
        }.standardOutput.asText.get().trim()
    ).getParentFile()
)

if (settings.hasProperty("buildReactNativeFromSource") && settings.buildReactNativeFromSource == "true") {
  include(":ReactAndroid")
  project(":ReactAndroid").projectDir = new File(reactNativePackage, "../ReactAndroid");
  include(":ReactAndroid:hermes-engine")
  project(":ReactAndroid:hermes-engine").projectDir = new File(reactNativePackage, "../ReactAndroid/hermes-engine");
}

include ':detox'
project(':detox').projectDir = new File(rootProject.projectDir, '../node_modules/detox/android/detox')
