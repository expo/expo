pluginManagement {
  repositories {
    mavenCentral()
    gradlePluginPortal()
    mavenLocal()
    google()
  }
}

include ':app'

apply from: new File(["node", "--print", "require.resolve('expo/package.json')"].execute(null, rootDir).text.trim(), "../scripts/autolinking.gradle")
apply from: new File(rootDir, "versioning_linking.gradle")

includeBuild('../react-native-lab/react-native/packages/react-native-gradle-plugin/')

include ':expoview'
include ':tools'
include ':ReactAndroid'
project(':ReactAndroid').projectDir = new File(rootDir, '../react-native-lab/react-native/packages/react-native/ReactAndroid')
include ':ReactAndroid:hermes-engine'
project(':ReactAndroid:hermes-engine').projectDir = new File(rootDir, '../react-native-lab/react-native/packages/react-native/ReactAndroid/hermes-engine')
include ':expo-modules-test-core'
project(':expo-modules-test-core').projectDir = new File(rootDir, '../packages/expo-modules-test-core/android')

// Include Expo modules that are not unimodules
include(":expo-random")
project(":expo-random").projectDir = new File("../packages/expo-random/android")

[
    // ADD_NEW_SUPPORTED_ABIS_HERE
    "abi48_0_0",
    "abi47_0_0",
].forEach({ abiVariant ->
  include ":expoview-$abiVariant"
  project(":expoview-$abiVariant").projectDir = new File(rootDir, "versioned-abis/expoview-$abiVariant")
})

useExpoModules([
    exclude : [
        'expo-module-template',
        'expo-module-template-local',
        'expo-in-app-purchases',
        'react-native-reanimated',
        'expo-dev-menu-interface',
        'expo-dev-menu',
        'expo-dev-launcher',
        'expo-dev-client',
        'expo-maps'
    ]
])

useVendoredModulesForSettingsGradle('unversioned')
useVendoredModulesForSettingsGradle('sdk48')
useVendoredModulesForSettingsGradle('sdk47')
