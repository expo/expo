apply plugin: 'com.android.library'
apply plugin: 'maven'

group = 'host.exp.exponent'
version = '0.7.2'

// Simple helper that allows the root project to override versions declared by this library.
def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

// Upload android library to maven with javadoc and android sources
configurations {
  deployerJars
}

// Creating sources with comments
task androidSourcesJar(type: Jar) {
  classifier = 'sources'
  from android.sourceSets.main.java.srcDirs
}

// Put the androidSources and javadoc to the artifacts
artifacts {
  archives androidSourcesJar
}

uploadArchives {
  repositories {
    mavenDeployer {
      configuration = configurations.deployerJars
      repository(url: mavenLocal().url)
    }
  }
}

android {
  compileSdkVersion safeExtGet("compileSdkVersion", 29)

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  defaultConfig {
    minSdkVersion safeExtGet("minSdkVersion", 21)
    targetSdkVersion safeExtGet("targetSdkVersion", 29)
    versionCode 18
    versionName '0.7.2'
  }

  lintOptions {
    abortOnError false
  }
}

if (new File(rootProject.projectDir.parentFile, 'package.json').exists()) {
  apply from: project(":unimodules-core").file("../unimodules-core.gradle")
} else {
  throw new GradleException(
      '\'unimodules-core.gradle\' was not found in the usual Flutter or React Native dependency locations. ' +
          'This package can only be used in such projects. Are you sure you\'ve installed the dependencies properly?')
}

dependencies {
  unimodule 'unimodules-core'
  unimodule 'unimodules-permissions-interface'
  unimodule 'unimodules-image-loader-interface'

  api 'androidx.core:core:1.2.0'
  api 'androidx.lifecycle:lifecycle-runtime:2.2.0'
  api 'androidx.lifecycle:lifecycle-process:2.2.0'
  api 'androidx.lifecycle:lifecycle-common-java8:2.2.0'

  api 'com.google.firebase:firebase-messaging:20.2.4'

  api 'me.leolin:ShortcutBadger:1.1.22@aar'
}
