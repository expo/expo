apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

buildscript {
  def expoModulesCorePlugin = new File(project(":expo-modules-core").projectDir.absolutePath, "ExpoModulesCorePlugin.gradle")
  if (expoModulesCorePlugin.exists()) {
    apply from: expoModulesCorePlugin
    applyKotlinExpoModulesCorePlugin()
  }

  // Simple helper that allows the root project to override versions declared by this library.
  ext.safeExtGet = { prop, fallback ->
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
  }

  // Ensures backward compatibility
  ext.getKotlinVersion = {
    if (ext.has("kotlinVersion")) {
      ext.kotlinVersion()
    } else {
      ext.safeExtGet("kotlinVersion", "1.8.10")
    }
  }

  repositories {
    mavenCentral()
  }

  dependencies {
    classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${getKotlinVersion()}")
  }
}

android {
  compileSdkVersion safeExtGet("compileSdkVersion", 33)
  namespace "expo.modules.devclient"
  defaultConfig {
    minSdkVersion safeExtGet('minSdkVersion', 21)
    targetSdkVersion safeExtGet("targetSdkVersion", 33)
    versionCode 1
    versionName "2.4.13"

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }
  lintOptions {
    abortOnError false
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
  }

  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_11.majorVersion
  }

  buildTypes {
    buildTypes.each {
      it.buildConfigField 'String', 'VERSION', "\"${defaultConfig.versionName}\""
    }

    releaseWithDevLauncher {
      initWith release
      matchingFallbacks = ['release', 'debug']
    }
  }

  sourceSets {
    releaseWithDevLauncher {
      setRoot 'src/debug'
    }
  }

  buildFeatures {
    viewBinding true
  }
  publishing {
    singleVariant("release") {
      withSourcesJar()
    }
  }
}

repositories {
  // ref: https://www.baeldung.com/maven-local-repository
  mavenLocal()
  maven {
    // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
    url "$rootDir/../node_modules/react-native/android"
  }
  maven {
    // Android JSC is installed from npm
    url "$rootDir/../node_modules/jsc-android/dist"
  }
  google()
}

dependencies {
  androidTestImplementation project(':expo-modules-core')
  androidTestImplementation project(":expo-dev-menu-interface")
  androidTestImplementation project(":expo-updates-interface")
  androidTestImplementation project(":expo-dev-menu")
  androidTestImplementation project(":expo-dev-launcher")
  androidTestImplementation project(":expo-manifests")

  //noinspection GradleDynamicVersion
  androidTestImplementation 'com.facebook.react:react-native:+'  // From node_modules

  androidTestImplementation('androidx.test.espresso:espresso-core:3.4.0')
  androidTestImplementation('androidx.test:core:1.4.0')
  androidTestImplementation('androidx.test:core-ktx:1.4.0')
  androidTestImplementation('androidx.test.ext:junit:1.1.3')
  androidTestImplementation('androidx.test.ext:junit-ktx:1.1.3')
  androidTestImplementation('androidx.test:runner:1.4.0')
  androidTestImplementation('androidx.test:rules:1.4.0')

  androidTestImplementation 'org.webkit:android-jsc:+'

  androidTestImplementation "io.insert-koin:koin-test:3.1.2"
  androidTestImplementation "io.insert-koin:koin-test-junit4:3.1.2"

  androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.3"
  androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1"

  androidTestImplementation "androidx.appcompat:appcompat:1.1.0"

  androidTestImplementation "com.google.truth:truth:1.1.2"
  androidTestImplementation 'io.mockk:mockk-android:1.12.3'

  // Fixes "e: java.lang.AssertionError: No such enum entry LIBRARY_GROUP_PREFIX in org.jetbrains.kotlin.ir.types.impl.IrSimpleTypeImpl@b254b575"
  // According to the https://stackoverflow.com/a/67736351
  implementation 'androidx.annotation:annotation:1.2.0'
}
