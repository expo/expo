apply plugin: 'com.android.library'

def useLegacyExpoModulesCorePlugin = {
  def expoModulesCorePlugin = new File(project(":expo-modules-core").projectDir.absolutePath, "ExpoModulesCorePlugin.gradle")
  apply from: expoModulesCorePlugin
  applyKotlinExpoModulesCorePlugin()
  useDefaultAndroidSdkVersions()
  useExpoPublishing()
}

try {
  apply plugin: 'expo-module-gradle-plugin'
} catch (e) {
  if (!e instanceof UnknownPluginException) {
    throw e
  }

  useLegacyExpoModulesCorePlugin()
}

def reactNativeArchitectures() {
    def value = project.getProperties().get("reactNativeArchitectures")
    return value ? value.split(",") : ["armeabi-v7a", "x86", "x86_64", "arm64-v8a"]
}

group = 'host.exp.exponent'
version = '15.0.2'

android {
  if (rootProject.hasProperty("ndkPath")) {
    ndkPath rootProject.ext.ndkPath
  }
  if (rootProject.hasProperty("ndkVersion")) {
    ndkVersion rootProject.ext.ndkVersion
  }

  namespace "expo.modules.gl"
  defaultConfig {
    versionCode 31
    versionName "15.0.2"

    externalNativeBuild {
      cmake {
        abiFilters (*reactNativeArchitectures())
        arguments "-DANDROID_STL=c++_shared"
      }
    }
  }

  externalNativeBuild {
    cmake {
      path "CMakeLists.txt"
    }
  }

  buildFeatures {
    prefab true
  }

  packagingOptions {
    // Gradle will add cmake target dependencies into packaging.
    excludes += [
        "**/libc++_shared.so",
        "**/libjsi.so",
    ]
  }

  configurations {
    extractHeaders
    extractJNI
  }
}

dependencies {
  compileOnly 'com.facebook.soloader:soloader:0.8.2'
  compileOnly 'com.facebook.react:react-android'
}
