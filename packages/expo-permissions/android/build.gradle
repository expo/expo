buildscript {
  repositories {
    google()
    jcenter()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:3.5.1'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.41"
  }
}


apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven'


group = 'host.exp.exponent'
version = '8.0.0-rc.0'

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

//Upload android library to maven with javadoc and android sources
configurations {
  deployerJars
}

//Creating sources with comments
task androidSourcesJar(type: Jar) {
  classifier = 'sources'
  from android.sourceSets.main.java.srcDirs
}

//Put the androidSources and javadoc to the artifacts
artifacts {
  archives androidSourcesJar
}

uploadArchives {
  repositories {
    mavenDeployer {
      configuration = configurations.deployerJars
      repository(url: mavenLocal().url)
    }
  }
}

android {
  compileSdkVersion safeExtGet("compileSdkVersion", 28)

  defaultConfig {
    minSdkVersion safeExtGet("minSdkVersion", 21)
    targetSdkVersion safeExtGet("targetSdkVersion", 28)
    versionCode 21
    versionName "8.0.0-rc.0"
  }
  lintOptions {
    abortOnError false
  }
  // Always show the result of every unit test when running via command line, even if it passes.
  testOptions.unitTests {
    includeAndroidResources = true
  }
}

repositories {
  mavenCentral()
  jcenter()
  google()
}

if (new File(rootProject.projectDir.parentFile, 'package.json').exists()) {
  apply from: project(":unimodules-core").file("../unimodules-core.gradle")
} else {
  throw new GradleException(
      "'unimodules-core.gradle' was not found in the usual Flutter or React Native dependency locations. " +
          "This package can only be used in such projects. Are you sure you've installed the dependencies properly?")
}

dependencies {
  unimodule 'unimodules-core'
  unimodule 'unimodules-permissions-interface'

  api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.41"
  api "androidx.appcompat:appcompat:1.0.2"
  
  compileOnly('com.facebook.react:react-native:+') {
    exclude group: 'com.android.support'
  }

  // Tests dependencies
  testImplementation 'junit:junit:4.12'
  testImplementation "org.jetbrains.kotlin:kotlin-test-junit:1.3.41"
  testImplementation 'org.mockito:mockito-core:2.19.1'
  testImplementation "org.robolectric:robolectric:4.3.1"
  testImplementation 'androidx.test:core-ktx:1.2.0'
  testImplementation "androidx.arch.core:core-testing:2.1.0"
  testImplementation 'androidx.test.ext:junit:1.1.1'
  testImplementation 'androidx.test.ext:truth:1.2.0'

  testCompile('com.facebook.react:react-native:+') {
    exclude group: 'com.android.support'
  }
}
