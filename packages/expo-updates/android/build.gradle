apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'maven-publish'

group = 'host.exp.exponent'
version = '0.11.6'

apply from: "../scripts/create-manifest-android.gradle"

buildscript {
  // Simple helper that allows the root project to override versions declared by this library.
  ext.safeExtGet = { prop, fallback ->
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
  }

  repositories {
    mavenCentral()
  }

  dependencies {
    classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${safeExtGet('kotlinVersion', '1.4.21')}")
  }
}

// Creating sources with comments
task androidSourcesJar(type: Jar) {
  classifier = 'sources'
  from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
  publishing {
    publications {
      release(MavenPublication) {
        from components.release
        // Add additional sourcesJar to artifacts
        artifact(androidSourcesJar)
      }
    }
    repositories {
      maven {
        url = mavenLocal().url
      }
    }
  }
}

android {
  compileSdkVersion safeExtGet("compileSdkVersion", 30)

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_1_8
  }

  defaultConfig {
    minSdkVersion safeExtGet("minSdkVersion", 21)
    targetSdkVersion safeExtGet("targetSdkVersion", 30)
    versionCode 31
    versionName '0.11.6'
    consumerProguardFiles("proguard-rules.pro")
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    // uncomment below to export the database schema when making changes
    /* javaCompileOptions {
      annotationProcessorOptions {
        arguments += ["room.schemaLocation":
                      "$projectDir/src/androidTest/schemas".toString()]
      }
    } */
  }
  lintOptions {
    abortOnError false
  }
  testOptions {
    unitTests.includeAndroidResources = true
  }
  sourceSets {
    androidTest.assets.srcDirs += files("$projectDir/src/androidTest/schemas".toString())
  }
}

dependencies {
  implementation project(':expo-modules-core')
  implementation project(':expo-structured-headers')
  implementation project(':expo-updates-interface')
  implementation project(':expo-manifests')
  implementation project(':expo-json-utils')
  //noinspection GradleDynamicVersion
  implementation "com.facebook.react:react-native:+"

  def room_version = "2.3.0"

  implementation "androidx.room:room-runtime:$room_version"
  kapt "androidx.room:room-compiler:$room_version"

  // force upgrade sqlite-jdbc to support building on aarch64 jdk of macos m1.
  // https://issuetracker.google.com/issues/174695268
  kapt "org.xerial:sqlite-jdbc:3.36.0"

  implementation("com.squareup.okhttp3:okhttp:3.12.1")
  implementation("com.squareup.okhttp3:okhttp-urlconnection:3.12.1")
  implementation("com.squareup.okio:okio:1.15.0")
  implementation("commons-io:commons-io:2.6")
  implementation("commons-fileupload:commons-fileupload:1.4")
  implementation("org.apache.commons:commons-lang3:3.9")

  testImplementation 'junit:junit:4.12'
  testImplementation 'androidx.test:core:1.0.0'
  testImplementation 'org.mockito:mockito-core:1.10.19'
  testImplementation 'io.mockk:mockk:1.12.0'

  androidTestImplementation 'androidx.test:runner:1.1.0'
  androidTestImplementation 'androidx.test:core:1.0.0'
  androidTestImplementation 'androidx.test:rules:1.1.0'
  androidTestImplementation 'org.mockito:mockito-android:3.7.7'
  androidTestImplementation 'io.mockk:mockk-android:1.12.0'
  androidTestImplementation "androidx.room:room-testing:$room_version"

  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${safeExtGet('kotlinVersion', '1.4.21')}"
  implementation "org.jetbrains.kotlin:kotlin-reflect:${safeExtGet('kotlinVersion', '1.4.21')}"
}
repositories {
  mavenCentral()
}

// [BEGIN] Workaround okhttp/okio compatibility issue
// Remove when we drop support for SDK 44
def okhttpPinnedArtifacts = []
configurations.implementation.getDependencies().removeIf { it ->
  if (['com.squareup.okhttp3', 'com.squareup.okio'].contains(it.group)) {
    okhttpPinnedArtifacts.add("${it.group}:${it.name}:${it.version}")
    return true
  }
  return false
}
okhttpPinnedArtifacts.each { artifact ->
  dependencies.add('compileOnly', artifact)
  dependencies.add('testImplementation', artifact)
  dependencies.add('androidTestImplementation', artifact)
  configurations.all {
    resolutionStrategy.force(artifact)
  }
}
// [END] Workaround okhttp/okio compatibility issue
