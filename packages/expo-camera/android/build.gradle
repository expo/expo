apply plugin: 'com.android.library'
apply plugin: 'maven'

group = 'host.exp.exponent'
version = '9.0.0'

// Simple helper that allows the root project to override versions declared by this library.
def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

//Upload android library to maven with javadoc and android sources
configurations {
  deployerJars
}

//Creating sources with comments
task androidSourcesJar(type: Jar) {
  classifier = 'sources'
  from android.sourceSets.main.java.srcDirs
}

//Put the androidSources and javadoc to the artifacts
artifacts {
  archives androidSourcesJar
}

uploadArchives {
  repositories {
    mavenDeployer {
      configuration = configurations.deployerJars
      repository(url: mavenLocal().url)
    }
  }
}

android {
  compileSdkVersion safeExtGet("compileSdkVersion", 29)

  defaultConfig {
    minSdkVersion safeExtGet("minSdkVersion", 21)
    targetSdkVersion safeExtGet("targetSdkVersion", 29)
    versionCode 29
    versionName "9.0.0"
  }
  lintOptions {
    abortOnError false
  }
  compileOptions {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
  }
}

repositories {
  mavenCentral()
  maven {
    url "$projectDir/maven"
  }
}

apply from: project(":unimodules-core").file("../unimodules-core.gradle")

dependencies {
  unimodule 'unimodules-core'
  unimodule 'unimodules-barcode-scanner-interface'
  unimodule 'unimodules-face-detector-interface'
  unimodule 'unimodules-permissions-interface'
  unimodule 'unimodules-file-system-interface'
  unimodule 'unimodules-image-loader-interface'
  unimodule 'unimodules-camera-interface'
  api "androidx.exifinterface:exifinterface:1.0.0"
  api 'com.google.android:cameraview:1.0.0'
}
